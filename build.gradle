plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.asciidoctor.jvm.convert' version '4.0.2'
}

group = 'com.hanium'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jacoco {
    toolVersion = "0.8.13"
    reportsDirectory = layout.buildDirectory.dir("reports/jacoco")
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MySQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // REST docs
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    // Asciidoctor 문서 변환
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

    // H2
    testImplementation 'com.h2database:h2:2.1.214'

    // security
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Reactive Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    // WebFlux
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // s3
    implementation 'software.amazon.awssdk:s3:2.25.29'

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api:2.1.1'

    // selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.34.0'

    // actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // logback
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

// Jacoco 제외 대상 설정
def snippetsDir = layout.buildDirectory.dir("generated-snippets")
ext.jacocoExclusions = [
        '**/*Dto*.class',
        '**/dto/**',
        '**/Q*.class'
]

tasks.named('test') {
    useJUnitPlatform()
    outputs.dir(snippetsDir)
    finalizedBy(tasks.named('jacocoTestReport'))
    finalizedBy(tasks.named('jacocoTestCoverageVerification'))
}

tasks.named('jacocoTestReport') {

    classDirectories.setFrom(
            files(classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExclusions)
            })
    )

    dependsOn(tasks.named('test'))
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco/html"))
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn(tasks.named('jacocoTestReport'))

    classDirectories.setFrom(
            files(tasks.named('jacocoTestReport').get().classDirectories.files.collect { dir ->
                fileTree(dir: dir, exclude: jacocoExclusions)
            })
    )

    // Test Coverage 설정
//    violationRules {
//        rule {
//            element = 'BUNDLE'
//            limit {
//                counter = 'LINE'
//                value   = 'COVEREDRATIO'
//                minimum = 0.40
//            }
//            limit {
//                counter = 'BRANCH'
//                value   = 'COVEREDRATIO'
//                minimum = 0.40
//            }
//        }
//    }
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn tasks.named('test')
}

tasks.named('bootJar') {
    dependsOn tasks.named('asciidoctor')
    from(layout.buildDirectory.dir("docs/asciidoc")) {
        into "static/docs"
    }
}

def queryDslSrcDir = 'build/generated/querydsl/'

sourceSets {
    main.java.srcDirs += [queryDslSrcDir]
}

clean {
    delete file(queryDslSrcDir)
}